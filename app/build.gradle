plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}

def room_version = "2.3.0"
def compose_version = "1.0.1"

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'  // use viewModel in compose
    implementation "androidx.activity:activity-compose:1.3.1"  // setContent, etc

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // navigation
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha06"

    // maps
    implementation 'com.google.android.libraries.maps:maps:3.1.0-beta'
    implementation 'com.google.maps.android:maps-v3-ktx:2.3.0'
    implementation 'com.google.maps.android:android-maps-utils-v3:1.3.1'


    // Compose
    implementation "androidx.compose.compiler:compiler:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version" // Tooling support (Previews, etc.)
    implementation "androidx.compose.foundation:foundation:$compose_version" // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.material:material:$compose_version" // Material Design
    implementation "androidx.compose.material:material-icons-core:$compose_version"        // Material design icons
    implementation "androidx.compose.material:material-icons-extended:$compose_version"    // Material design icons
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version" // Integration with observables
    //androidTestImplementation "androidx.ui:ui-test:$compose_version" // UI Tests

//    testImplementation 'junit:junit:4.+'
//    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if(keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def secretProperties = new Properties()
secretProperties.load(new FileInputStream(rootProject.file('secrets.properties')))

def versionProperties = VersionProperties.load(rootProject.file('version.properties'))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile (keystoreProperties['storeFile'] != null ? file(keystoreProperties['storeFile']) : null)
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.trackme.android"
        minSdkVersion 28
        targetSdkVersion 30
        versionCode versionProperties.versionCode
        versionName versionProperties.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [MAPS_API_KEY:secretProperties.getProperty("MAPS_API_KEY")]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerVersion kotlin_version
        kotlinCompilerExtensionVersion compose_version
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    // Handle API secrets
    //secrets {
    //    propertiesFileName 'secrets.properties'
    //}
}

//tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
//    kotlinOptions {
//        jvmTarget = "1.8"
//        freeCompilerArgs += ["-Xallow-jvm-ir-dependencies", "-Xskip-prerelease-check"]
//    }
//}